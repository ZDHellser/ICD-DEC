// Needs more.... Imp-se~
// Huh. ICD didn't change much. I'll keep this here just in case.

ACTOR ICD_Impse	replaces DoomImp
{
	Health 64
	Radius 20
	Height 56
	Mass 400
	Speed 8
	PainChance 200
	MinMissileChance 204
	Monster
	+FLOORCLIP
	+DOHARMSPECIES
	SeeSound "imp/sight"
	PainSound "imp/pain"
	DeathSound "imp/death"
	ActiveSound "imp/active"
	HitObituary "$OB_IMPHIT"
	Obituary "$OB_IMP"
	scale 1.1
	States
	{
		Spawn:
			TROO AA 5 A_Look
			Loop
		See:
			TROO A 3 A_Chase
			TROO A 2 A_Chase
			TROO B 3 A_Chase
			TROO B 2 A_Chase
			TROO C 3 A_Chase
			TROO C 2 A_Chase
			TROO D 3 A_Chase
			TROO D 2 A_Chase
			Loop
		Melee:
		Missile:
			TROO EF 7 A_FaceTarget
			TROO G 5 A_CustomComboAttack("ICD_ImpBall", 32, (random(8,32)), "imp/melee")
			Goto See
		Pain:
			TROO H 2
			TROO H 2 A_Pain
			Goto See
		Death:
			TROO I 7
			TROO J 6 A_Scream
			TROO K 5
			TROO L 4 A_NoBlocking
			TROO M -1
			Stop
		XDeath:
			TROO N 5
			TROO O 5 A_XScream
			TROO P 5
			TROO Q 5 A_NoBlocking
			TROO RST 5
			TROO U -1
			Stop
		Raise:
			TROO MLKJI 8
			Goto See
	}
}

ACTOR ICD_ImpBall replaces DoomImpBall
{
	Game Doom
	SpawnID 10
	Radius 6
	Height 8
	Speed 10
	FastSpeed 20
	Damage (random(3,24))
	Projectile
	+RANDOMIZE
	RenderStyle translucent
	Scale 0.82
	Alpha 0.65
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	States
	{
		Spawn:
			BAL1 AB 3 Bright Light("ICD_ImpBall") A_SpawnItem("ICD_ImpBallsPuff")
			Loop
		Death:
			BAL1 C 6 Bright Light("ICD_ImpBallExp1")
			BAL1 D 6 Bright Light("ICD_ImpBallExp2")
			BAL1 E 6 Bright Light("ICD_ImpBallExp3")
			Stop
	}
}

//Unable to find "ImpBalls", this actor cannot reproduce.
ACTOR ICD_ImpBallsPuff
{
	scale 0.62
	radius 8
	height 16
	renderstyle translucent
	alpha 0.3
	+NOBLOCKMAP
	+NOGRAVITY
	States
	{
		Spawn:
			TNT1 A 1
			BAL1 C 3 bright
			BAL1 D 3
			BAL1 E 3
			stop
	}
}