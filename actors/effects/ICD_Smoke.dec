/*---------------------------------------------------------------------
 WEAPON SMOKE
 DDF NAMES:
	SHOTGUN_SMOKE				[DDFTHING]
	PISTOL_SMOKE				[DDFTHING]
	M16_SMOKE					[DDFTHING]
	UZI_SMOKE					[DDFTHING]
	MINI_SMOKE					[DDFTHING]
	RPUFF						[DDFTHING]
---------------------------------------------------------------------*/

ACTOR ICD_SHOTGUN_SMOKE : ICD_GunSmokeBase
{
	Scale 0.3
}

ACTOR ICD_PISTOL_SMOKE : ICD_GunSmokeBase
{
	Speed 3
	scale 0.25
}

ACTOR ICD_M16_SMOKE : ICD_GunSmokeBase
{
	Speed 6
	Scale 0.5
}

ACTOR ICD_UZI_SMOKE : ICD_GunSmokeBase
{
	Speed 4
	Scale 0.4
}

ACTOR ICD_MINI_SMOKE : ICD_GunSmokeBase
{
	Speed 5
	Scale 0.4
}

ACTOR ICD_RPUFF : ICD_GunSmokeBase
{
	Scale 0.5
	Alpha 0.5
	States
	{
		Spawn:
			TNT1 A 0
			SMOK A 5
			SMOK B 5 A_FadeOut
			SMOK C 5 A_FadeOut
			SMOK D 5 A_FadeOut
			SMOK E 5
			stop
	}

}

ACTOR ICD_QPUFF : ICD_GS_EmptyCasingSmoke
{
	scale 0.7
}

ACTOR ICD_PUFF : BulletPuff
{
	VSpeed 0.5
	+NOEXTREMEDEATH
	Renderstyle Translucent
	Alpha 1
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("icd/impc1", 6)
			PUFF A 2 BRIGHT Light("ICD_Sparks")
			PUFF B 2 BRIGHT
			TNT1 AAAA 0 A_SpawnItemEx("ICD_BulletPuffSpark", 0,0,0,random(-5,5),random(-5,5),random(-5,5),0,SXF_NOCHECKPOSITION)
			TNT1 AA 0 A_SpawnItemEx("ICD_BLACK_SPARKS", 0,0,0,random(-5,5),random(-5,5),random(-5,5),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_Jump(179, 3)
			TNT1 A 0 A_Jump(179, 2)
			TNT1 A 0 A_PlaySound("world/effect/richochet", 5)
			TNT1 A 0 A_FadeOut(0.8,0)
			SMKE BCDEFGH 2
			Stop
	}
}

ACTOR ICD_SPUFF : BulletPuff
{
	VSpeed 0.5
	+NOEXTREMEDEATH
	Renderstyle Translucent
	Alpha 0.2
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("icd/impc1", 6)
			TNT1 AAAA 0 A_SpawnItemEx("ICD_BLACK_SPARKS", 0,0,0,random(-5,5),random(-5,5),random(-5,5),0,SXF_NOCHECKPOSITION,75)
			SMKE ABCDEFGH 2
			Stop
	}
}

ACTOR ICD_BulletPuffSpark
{
	Height 8
	Radius 8
	Speed 32
	Mass 2
	+MISSILE
	+MOVEWITHSECTOR
	+THRUACTORS
	+NOTELEPORT
	+NODAMAGETHRUST
	-NOGRAVITY
	BounceType Doom
	BounceFactor 0.4
	//renderstyle add
	States
	{
		Spawn:
			TNT1 A 0
			FRAG A 1 BRIGHT
			TNT1 A 0 A_Jump(179, 2)
			TNT1 A 0 A_Explode(1, 32, 1, 0, 1)
			FRAG A 1 BRIGHT
			FRAG AAAA 2 BRIGHT A_FadeOut
			Stop
	}
}

ACTOR ICD_BLACK_SPARKS
{
	Height 8
	Radius 8
	Speed 10
	Mass 2
	BounceType "None"
	BounceFactor 0.5
	WallBounceFactor 0.5
	BounceCount 4
	+MISSILE
	-NOGRAVITY
	+THRUACTORS
	+MOVEWITHSECTOR
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+NOTELEPORT
	+FORCEXYBILLBOARD
	renderstyle translucent
	Alpha 0.75
	States
	{
		Spawn:
			TNT1 A 0
			FRAG B 1
			Loop
		Death:
			FRAG B 350
			FRAG BBBB 105 A_FadeOut
			Stop
	}
}

/*---------------------------------------------------------------------
 EXPLOSION SMOKE
 DDF NAME: EXPLOSION_SMOKE 		[DDFATK]
---------------------------------------------------------------------*/

ACTOR ICD_Explosion_Smoke : ICD_GunSmokeBase
{
	Scale 2.3
	Alpha 0.7
	States
	{
		Spawn:
			TNT1 A 0
			PUF5 B 5
			PUF5 CDEFGHIJKLMNOP 7 A_FadeOut(0.05)
			stop
	}
}

/*---------------------------------------------------------------------
 FLARE SMOKE
 DDF NAME: PLAYER_FLARE_SMOKE	[DDFATK]
---------------------------------------------------------------------*/

ACTOR ICD_FLARE_SMOKE : ICD_GunSmokeBase 
{
	Speed 0.5
	Scale 0.13
	Alpha 0.4
	States
	{
		Spawn:
			RLSR C 6 BRIGHT
			TNT1 A 0 A_Jump(175, "SpawnJump")
			TNT1 A 0 A_PlaySound("icd/flare2")
			TNT1 A 0 A_JumpIf(waterlevel==3,"Bubbles")
			TNT1 A 0 A_SpawnItemEx("ICD_Flare_Sparks_Red",0,0,0,frandom(-2,2), frandom(-2,2), frandom(2,3),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("ICD_Flare_Sparks_Yellow",0,0,0,frandom(-2,2), frandom(-2,2), frandom(2,3),0,SXF_NOCHECKPOSITION)
			SMOK A 1 ThrustThingZ(0, 1, 0, 1)
			SMOK A 2
			SMOK A 0 // Flare2
		SpawnJump:
			TNT1 A 0 A_JumpIf(waterlevel==3,"Bubbles")
			SMOK A 2
			SMOK B 4 A_FadeOut
			SMOK C 4 A_FadeOut
			SMOK D 4 A_FadeOut
			SMOK E 4 A_FadeOut
			Stop
		Bubbles:
			TNT1 A 0 A_SetScale(0.5)
			TNT1 A 0 ThrustThingZ(0,1,0,1)
			BUBB A 1 A_Weave(random(-2, 2),0,0,0)
			TNT1 A 0 A_JumpIf(waterlevel<2, "Death")
			TNT1 A 0 A_Jump(1, "Death")
			Loop
		Death:
			BUBB A 0
			Stop
	}
}

ACTOR ICD_FlareBubble : ICD_FLARE_SMOKE
{
	Speed 0.5
	Scale 0.3
	Alpha 0.8
	States
	{
		Spawn:
			TNT1 A 0 ThrustThingZ(0,1,0,1)
			BUBB A 1 A_Weave(random(2, 2),0,0,0)
			TNT1 A 0 A_JumpIf(waterlevel<2, "Death")
			TNT1 A 0 A_Jump(1, "Death")
			Loop
		Death:
			BUBB A 0
			Stop
	}
}

ACTOR ICD_FLARE_SMOKE2 : ICD_FLARE_SMOKE
{
	Scale 0.6
}

/*---------------------------------------------------------------------
 CHAINSAW, VROOM VROOM!
---------------------------------------------------------------------*/

ACTOR ICD_CHAINSAW_SMOKE_Spawner : ICD_ShellSpawner
{
	 States
	 {
		Spawn:
			TNT1 A 1
		Death:
			TNT1 A 0 A_SpawnProjectile("ICD_CHAINSAW_SMOKE",0,0,270+random(-5,5),2,random(-10,10))
			Stop
	 }
}

ACTOR ICD_CHAINSAW_SMOKE2_Spawner: ICD_CHAINSAW_SMOKE_Spawner
{
	States
	{
		Spawn:
			TNT1 A 1
		Death:
			TNT1 A 0 A_SpawnProjectile("ICD_CHAINSAW_SMOKE2",0,0,270+random(-5,5),2,random(-10,10))
			Stop
	}
}

ACTOR ICD_CHAINSAW_SMOKE: ICD_GunSmokeBase
{
	Scale 0.17
	Alpha 0.10
	BounceFactor 0.1
	Bouncetype Doom
	Speed 0.5
	States
	{
		Spawn:
			TNT1 A 0 Nodelay A_ScaleVelocity(0.5)
			TNT1 A 0 A_Jump(127, 8)
			PUF2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 4
			PUF3 ABC 4
			stop
	}
}

ACTOR ICD_CHAINSAW_SMOKE2: ICD_CHAINSAW_SMOKE
{
	States
	{
		Spawn:
			TNT1 A 0 Nodelay A_ScaleVelocity(1.3)
			TNT1 A 0 A_Jump(127, 8)
			PUF2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 4
			PUF3 ABC 4
			stop
	}
}

ACTOR ICD_FIRE_SMOKE4
{
	Height 8
	Radius 8
	Scale 0.6
	Speed 0.05
	Renderstyle Translucent
	Alpha 0.3
	Projectile
	+NOCLIP
	+NOBLOCKMAP
	+MISSILE
	+NOGRAVITY
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_Jump(127, "SpawnJump1")
			PUF5 E 3
			PUF5 F 3
			PUF5 G 4
		SpawnJump1:
			TNT1 A 0
			PUF5 IJ 5
			PUF5 K 6
			PUF5 LM 6
			PUF5 NOP 7
			Stop
	}
}
/*---------------------------------------------------------------------
Ah, Sparks. Knife Sparks, Sparks, just Sparks. Not all here though.
Will be in the near future.
---------------------------------------------------------------------*/

ACTOR ICD_SPARKS
{
	Radius 8
	Height 16
	+NOINTERACTION
	States
	{
		Spawn:
			SPUF A 2 Bright
			SPUF B 2 Bright A_PlaySound("icd/buzz")
			SPUF C 2 Bright A_Explode(20,32)
			SPUF D 2 Bright
			Stop
	}
}