ACTOR ICD_Remington : BaseWeapon
{
	Game Doom
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoType "ICD_RemingtonMagazine"
	Weapon.AmmoType2 "ICD_Shells"
	Inventory.PickupMessage "$GotRemington"
	Tag "$TAG_Remington"
	Weapon.UpSound "icd/weapu?"
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	Tag "Remington Shotgun"
	States
	{
		Select:
			TNT1 A 0 A_JumpIfInventory("ICD_RemingtonUpgrade",1,"Upgrade")
			SHTG A 1 A_Raise
			Goto Select+1
		Deselect:
			SHTG A 1 A_Lower
			Loop
		Ready:
			TNT1 A 0 A_JumpIfInventory("ICD_RemingtonUpgrade",1,"Upgrade")
			SHTG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("ICD_RemingtonMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Shells", 1, "Reload")
			Goto Ready
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_Gunflash
			TNT1 A 0 A_PlaySound("icd/shotg1", CHAN_WEAPON)
			SHTG A 1 A_FireBullets(5, 4, 9, random(5,16), "ICD_SPUFF", FBF_NORANDOM | FBF_USEAMMO, 1024)
			TNT1 AAAAA 0 A_FireProjectile("ICD_Gun3_Sparks", frandom(-5,5),0,5,0,0,frandom(-5,5))
			TNT1 AAAA 0 A_FireProjectile("ICD_SHOTGUN_SMOKE", frandom(-9,9),0,5,0,0,frandom(-5,5))
			SHTG A 2
			SHTG E 2
			SHTG A 3
			SHTG B 2
			SHTG C 2
			SHTG Q 4
			SHTG R 2 A_PlaySound("icd/srack", 6)
			SHTG R 2 A_FireProjectile("ICD_ShotgunShellSpawner1", 0, 0, 5, -4)
			SHTG Q 4
			SHTG C 3
			SHTG B 2
			SHTG A 1
			SHTG A 3 A_Refire
			Goto Ready
		Reload:
			SHTG A 1 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("ICD_RemingtonMagazine",8,"Ready")
			TNT1 A 0 A_JumpIfInventory("ICD_Shells", 1, 1)
			Goto Ready
			TNT1 A 0 A_PlaySound("icd/click2")
			SHTG A 1
			SHTG E 2
			SHTG A 2
			SHTG G 3
			SHTG H 3
			TNT1 A 4
			SHTG I 3
		Reload.Actual:
			SHTG J 2
			SHTG M 3
			SHTG K 3
			SHTG L 3
			TNT1 A 0 A_PlaySound("icd/dbload", 6)
			TNT1 A 0 A_TakeInventory("ICD_Shells",1,TIF_NOTAKEINFINITE)
			TNT1 A 0 A_GiveInventory("ICD_RemingtonMagazine",1)
			SHTG N 3 A_WeaponReady(WRF_NOBOB)
			SHTG J random(2,4) A_WeaponReady(WRF_NOBOB)
			TNT1 A 0 A_JumpIfInventory("ICD_RemingtonMagazine", 8, "Reload.Done")
			TNT1 A 0 A_JumpIfInventory("ICD_Shells", 1, "Reload.Actual")
		Reload.Done:
			SHTG I 2
			TNT1 A 2
			SHTG H 3
			SHTG G 3
			SHTG A 3
			SHTG B 2
			SHTG C 3 A_PlaySound("icd/srack", 6)
			SHTG D 3
			SHTG C 4
			SHTG B 2
			SHTG A 1
			Goto Ready
		Flash:
			SHTF B 1 bright	A_Light(1)
			SHTF A 2 bright A_Light(2)
			TNT1 A 0 A_Light(0)
			Goto LightDone
		Upgrade:
			TNT1 A 0 A_SelectWeapon("ICD_FlakRemington")
			Goto Deselect
	}
}

ACTOR ICD_RemingtonUpgrade : CustomInventory
{
	Inventory.MaxAmount 1
	Inventory.Icon SHOTA0
	+INVENTORY.IGNORESKILL
	States
	{
		Use:
			TNT1 A 0
			FAIL
	}
}

ACTOR ICD_RemingtonMagazine : Ammo
{
	Inventory.MaxAmount 8
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 8
	Inventory.Icon SHELA0
	+INVENTORY.IGNORESKILL
}
