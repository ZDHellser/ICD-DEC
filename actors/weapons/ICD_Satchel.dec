
ACTOR ICD_Satchel : BaseWeapon
{
	Game Doom
	Weapon.UpSound "icd/weapu?"
	Tag "$TAG_SATCHEL"
	Inventory.PickupMessage "$GotSatchel"
	Weapon.AmmoType "ICD_SatchelAmmo"
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Select:
			CHRG F 1 A_Raise
			Loop
		Deselect:
			CHRG F 1 A_Lower
			Loop
		Ready:
			CHRG F 1 A_WeaponReady
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("ICD_SatchelAmmo", 1, 1)
			Goto Ready
			TNT1 A 0 A_TakeInventory("ICD_SatchelAmmo", 1)
			CHRG F 12 A_GunFlash("Overlay")
			CHRG J 4 A_PlaySound("icd/click")
			CHRG D 4 A_PlaySound("icd/satch2")
			CHRG F 4 A_FireProjectile("ICD_ThrownSatchel", 0, 1, -4, 0, 0, 4)
			CHRG F 4 // Refire
			Goto Ready
		Overlay:
			CHRG ABCA 4
			TNT1 A 10
			Stop
		AltFire:
			CHRG E 4 A_PlaySound("icd/click")
			CHRG E 1 A_PlaySound("icd/satch2")
			TNT1 A 0 A_GiveInventory("ICD_SatchelMechanic", 1)
			CHRG D 20 // Boom?
			TNT1 A 0 A_TakeInventory("ICD_SatchelMechanic", 1)
			CHRG D 5 // Refire
			Goto Ready
	}
}

ACTOR ICD_SatchelMechanic : CustomInventory
{
	Inventory.MaxAmount 1
	+INVENTORY.IGNORESKILL
	+INVENTORY.AUTOACTIVATE
	States
	{
		Pickup:
		Use:
			TNT1 A 1
			TNT1 A 0 A_TakeInventory("ICD_SatchelMechanic", 1)
			FAIL
	}
}

/*
Alright, here's the idea. The player uses the detonator. which gives
the player a token. Missiles automatically targets the player. So.
A_JumpIfInTargetInventory, the token. THEN BOOM!
*/

ACTOR ICD_ThrownSatchel
{
	Radius 4
	Height 4
	Speed 16
	BounceType "Doom"
	Scale 0.8
	PROJECTILE
	SeeSound "icd/throw"
	+USEBOUNCESTATE
	+BOUNCEONACTORS
	+SHOOTABLE
	+DONTCORPSE
	-NOGRAVITY
	BounceType Doom
	BounceFactor 0.5
	States
	{
		Spawn:
			CHRG GGHH 1 A_JumpIfInTargetInventory("ICD_SatchelMechanic", 1, "True.Death")
			Loop
		Bounce:
			TNT1 A 0 A_PlaySound("icd/satch1")
			CHRG GGHHHGG 1 A_JumpIfInTargetInventory("ICD_SatchelMechanic", 1, "True.Death")
			Goto Spawn
		Death:
			TNT1 A 0 A_SpawnItemEx("ICD_SatchelOnGround", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
			Stop
		True.Death:
			TNT1 A 0 A_ChangeFlag("NOGRAVITY", 1)
			TNT1 A 0 A_Stop
			TNT1 A 0 A_PlaySound("weapons/boom?")
			TNT1 A 0 A_Explode(328)
			TNT1 A 0 A_SpawnItemEx("ICD_SatchelExplosion")
			TNT1 A 5 A_SpawnItemEx("ICD_Explosion_Smoke")
			Stop
	}
}

ACTOR ICD_SatchelOnGround
{
	Health 10
	Radius 4
	Height 4
	Scale 0.8
	Mass 20
	+SHOOTABLE
	+SOLID
	+THRUACTORS
	+MOVEWITHSECTOR
	+NOBLOOD
	States
	{
		Spawn:
			CHRG G 1 A_JumpIfInTargetInventory("ICD_SatchelMechanic", 1, "Death")
			Loop
		Death:
			TNT1 A 0 A_ChangeFlag("NOGRAVITY", 1)
			TNT1 A 0 A_Stop
			TNT1 A 0 A_PlaySound("weapons/boom?")
			TNT1 A 0 A_Explode(328)
			TNT1 A 0 A_SpawnItemEx("ICD_SatchelExplosion")
			TNT1 A 5 A_SpawnItemEx("ICD_Explosion_Smoke")
			Stop
	}
}

ACTOR ICD_SatchelExplosion
{
	+NOINTERACTION
	Renderstyle Translucent
	Scale 1.5
	Alpha 0.8
	States
	{
		Spawn:
			EXP3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Bright A_FadeOut(0.019)
			Stop
	}
}