ACTOR ICD_Silenced : BaseWeapon
{
	Game Doom
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoType "ICD_SilencedPistolMagazine"
	Weapon.AmmoGive2 6
	Weapon.AmmoType2 "ICD_Bullets"
	Obituary "$OB_MPPISTOL"
	Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
	Tag "$TAG_SILENCED"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Ready:
			SPIS A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Deselect:
			SPIS A 1 A_Lower
			Loop
		Select:
			TNT1 A 0 A_PlaySound("icd/item2")
			SPIS A 2 A_Raise
			SPIS A 2 A_Raise
			SPIS A 20
		Select.Loop:
			SPIS A 1 A_Raise
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("ICD_SilencedPistolMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload")
			Goto Ready
			TNT1 A 0 A_PlaySound("icd/silenc", CHAN_WEAPON)
			SPIS B 2 Bright A_FireBullets(0.5, 0.5, 1, random(5,20), "ICD_PUFF", FBF_NORANDOM | FBF_USEAMMO, 1024)
			TNT1 A 0 A_FireProjectile("ICD_PISTOL_SMOKE", frandom(-5,5),0,3.5,2.8,0,frandom(-5,5))
			TNT1 A 0 A_FireProjectile("ICD_PistolCasingSpawner", 0, 0, 12, -4)
			SPIS C 2
			SPIS H 2
			SPIS A 0 A_Jump(127, 2)
			SPIS A 1
			SPIS A 2
			SPIS A 2 A_ReFire
			Goto Ready
		AltFire:
			TNT1 A 0 A_JumpIfInventory("ICD_SilencedPistolMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload")
			Goto Ready
			SPIS A 2
			TNT1 A 0 A_PlaySound("icd/silenc", CHAN_WEAPON)
			SPIS B 2 Bright A_FireBullets(2.3, 2.3, 1, random(5,20), "ICD_PUFF", FBF_NORANDOM | FBF_USEAMMO, 1024)
			TNT1 A 0 A_TakeInventory("ICD_SilencedPistolMagazine",1)
			TNT1 A 0 A_FireProjectile("ICD_PISTOL_SMOKE", frandom(-5,5),0,3.5,2.8,0,frandom(-5,5))
			SPIS C 2 A_FireProjectile("ICD_PistolCasingSpawner", 0, 0, 12, -4)
			SPIS A 0 A_ReFire
			SPIS A 5
			Goto Ready
		Reload:
			SPIS A 1 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("ICD_SilencedPistolMagazine",12,"Ready")
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets", 1, 1)
			Goto Ready
			SPIS C 2
			SPIS D 1 A_PlaySound("icd/pout")
			SPIS D 8 A_FireProjectile("ICD_EmptyPistolClipDrop",frandom(-6,6),0,2,-8,0,frandom(-3,3))
			SPIS F 6
		Reload.InfoPhase:
			TNT1 A 0 A_TakeInventory("ICD_Bullets",1)
			TNT1 A 0 A_GiveInventory("ICD_SilencedPistolMagazine",1)
			TNT1 A 0 A_JumpIfInventory("ICD_SilencedPistolMagazine",12,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload.Restart")
			PISR G 3 A_PlaySound("icd/pin")
			PISR H 3
			PISR L 4
			PISR I 4 A_PlaySound("icd/pslide")
			PISR JK 2
			SPIS A 4
			Goto Ready
		Reload.Restart:
			TNT1 A 0
			Goto Reload.InfoPhase
		Spawn:
			SPIS A -1
			Stop
	}
}

ACTOR ICD_SilencedPistolMagazine : Ammo
{
	Inventory.MaxAmount 12
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 12
	Inventory.Icon CLIPB0
	+INVENTORY.IGNORESKILL
}