ACTOR ICD_Pistol : BaseWeapon
{
	Game Doom
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoType "ICD_PistolMagazine"
	Weapon.AmmoGive2 6
	Weapon.AmmoType2 "ICD_Bullets"
	Obituary "$OB_MPPISTOL"
	Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
	Tag "$TAG_PISTOL"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Ready:
			PIST A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Deselect:
			PIST A 1 A_Lower
			Loop
		Select:
			PIST A 0 A_PlaySound("icd/holstr")
			PIST AAAAA 1 A_Raise
			PIST A 0 A_PlaySound("icd/hammr")
			PIST A 1 A_Raise
			Goto Select+7
		Fire:
			TNT1 A 0 A_JumpIfInventory("ICD_PistolMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload")
			Goto Ready
			TNT1 A 0 A_PlaySound("icd/1911", CHAN_WEAPON)
			TNT1 A 0 A_AlertMonsters
			PIST B 2 Bright A_FireBullets(1, 1, 1, random(5,25), "ICD_PUFF", FBF_NORANDOM | FBF_USEAMMO, 1024)
			TNT1 A 0 A_FireProjectile("ICD_PISTOL_SMOKE", frandom(-5,5),0,3.5,2.4,0,frandom(-5,5))
			TNT1 A 0 A_FireProjectile("ICD_PistolCasingSpawner", 0, 0, 12, -4)
			PIST C 2
			PIST H 2
			PIST A 0 A_Jump(127, 2)
			PIST A 1
			PIST A 1
			PIST A 2 A_ReFire
			Goto Ready
		AltFire:
			TNT1 A 0 A_JumpIfInventory("ICD_PistolMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload")
			Goto Ready
			GPIS A 2
			TNT1 A 0 A_PlaySound("icd/1911", CHAN_WEAPON)
			TNT1 A 0 A_AlertMonsters
			GPIS B 2 Bright A_FireBullets(2, 2, 1, random(5,25), "ICD_PUFF", FBF_NORANDOM, 1024)
			TNT1 A 0 A_TakeInventory("ICD_PistolMagazine",1)
			TNT1 A 0 A_FireProjectile("ICD_PISTOL_SMOKE", frandom(-5,5),0,random(1,3),2.4,0,frandom(-5,5))
			GPIS C 2 A_FireProjectile("ICD_PistolCasingSpawner", 0, 0, 6, -4)
			GPIS A 0 A_ReFire
			GPIS A 5
			Goto Ready
		Reload:
			PIST A 1 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("ICD_PistolMagazine",12,"Ready")
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets", 1, 1)
			Goto Ready
			PIST C 2
			PIST D 1 A_PlaySound("icd/pout")
			PIST D 8 A_FireProjectile("ICD_EmptyPistolClipDrop",frandom(-6,6),0,2,-8,0,frandom(-3,3))
			PIST F 6
		Reload.InfoPhase:
			TNT1 A 0 A_TakeInventory("ICD_Bullets",1)
			TNT1 A 0 A_GiveInventory("ICD_PistolMagazine",1)
			TNT1 A 0 A_JumpIfInventory("ICD_PistolMagazine",12,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload.Restart")
			PISR A 3 A_PlaySound("icd/pin")
			PISR B 3
			PISR F 4
			PISR C 4 A_PlaySound("icd/pslide")
			PISR DE 2
			PIST A 4
			Goto Ready
		Reload.Restart:
			TNT1 A 0
			Goto Reload.InfoPhase
		Spawn:
			PIST A -1
			Stop
	}
}

ACTOR ICD_PistolMagazine : Ammo
{
	Inventory.MaxAmount 12
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 12
	Inventory.Icon CLIPB0
	+INVENTORY.IGNORESKILL
}

ACTOR ICD_EmptyPistolClipDrop
{
	Height 4
	Radius 4
	+MISSILE
	-NOGRAVITY
	+THRUACTORS
	+MOVEWITHSECTOR
	+NOTELEPORT
	+FORCEXYBILLBOARD
	Speed 2
	Scale 0.6
	States
	{
		Spawn:
			CLP1 C 2
			Loop
		Death:
			TNT1 A 0 A_PlaySound("icd/clp1a")
			CLP1 BEAFB 2
			CLP1 D 400
			CLP1 DDDD 105 A_FadeOut
			Stop
	}
}