ACTOR ICD_FlakRemington : BaseWeapon
{
	Game Doom
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoType "ICD_FlakRemingtonMagazine"
	Weapon.AmmoType2 "Shell"
	Inventory.PickupMessage "$GotRemington"
	Tag "$TAG_FLAKSHOTGUN"
	Obituary "%k flakked %o."
	Weapon.UpSound "icd/weapu?"
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	States
	{
		Select:
			TNT1 A 0 A_TakeInventory("ICD_Remington")
			REMG A 1 A_Raise
			Loop
		Deselect:
			REMG A 1 A_Lower
			Loop
		Ready:
			REMG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Fire:
			//CheckReload
			TNT1 A 0 A_AlertMonsters
			REMG A 1 A_PlaySound("icd/shotg3", CHAN_WEAPON)
			REMG AAAA 0 A_FireProjectile("ICD_Gun3_Sparks", frandom(-5,5),0,5,0,0,frandom(-5,5))
			REMG AAA 0 A_FireProjectile("ICD_SHOTGUN_SMOKE", frandom(-9,9),0,5,0,0,frandom(-5,5))
			REMG AAAAAAAAA 0 A_FireProjectile("ICD_FLAK", frandom(-12,12),0,5,0,0,frandom(-5,5))
			REMG A 2
			REMG Q 2
			REMG A 3
			REMG C 2
			REMG D 3
			REMG E 3 A_PlaySound("icd/frack", CHAN_BODY)
			REMG F 4 // Shell
			REMG E 4
			REMG D 4
			REMG C 3
			REMG A 1 A_ReFire
			Goto Ready
		Flash:
			SHTF B 1 bright	A_Light(1)
			SHTF A 2 bright A_Light(2)
			TNT1 A 0 A_Light(0)
			Goto LightDone
	}
}

ACTOR ICD_FLAK
{
	Height 8
	Radius 8
	Speed 60
	Mass 2
	Damage (20)
	+MISSILE
	+MOVEWITHSECTOR
	//+BOUNCEONACTORS
	+NOTELEPORT
	-NOGRAVITY
	BounceType Doom
	BounceFactor 0.4
	BounceSound "weapons/flak/bounce"
	renderstyle add
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("NULL")
			FRAG A 2 BRIGHT A_Explode(30,32,XF_HURTSOURCE)
			FRAG A 2 BRIGHT A_SpawnItem("ICD_QPUFF")
			FRAG A 2 BRIGHT
			Goto Spawn+2
		 Death:
			stop
	}
}

ACTOR ICD_FlakRemingtonMagazine : Ammo
{
	Inventory.MaxAmount 8
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 8
	Inventory.Icon SHOTA0
	+INVENTORY.IGNORESKILL
}

